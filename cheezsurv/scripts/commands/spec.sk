on skript start:
  set {spec.disabled} to false if {spec.disabled} is not set

on join:
  if {%player%.spec.enabled} is not set:
    set {%player%.spec.enabled} to true

command /togglespec <text>:
  aliases: /togglesp
  trigger:
    if executor is not op:
      send "§cInsufficient permissions!"
      stop
    set {spec.disabled} to false if {spec.disabled} is not set
    if {spec.disabled} is false:
      set {spec.disabled} to true
      broadcast "§3The §b/spec §3command has been disabled by §b%executor% §3due to reason §b%arg-1%§3."
    else if {spec.disabled} is true:
      set {spec.disabled} to false
      broadcast "§3The §b/spec §3command has been reenabled by §b%executor% §3due to reason §b%arg-1%§3."


command /spec [<text>]:
  executable by: players
  aliases: /sp
  trigger:
    if player's gamemode is survival:
      delete {%player%.spec.loc}
      delete {%player%.spec.mode}

    if {%player%.spec.mode} is not set: # survival
      if {spec.disabled} is true:
        send "§c/spec is currently disabled. Ask an admin if this is a mistake."
        stop
      if player is not flying:
        block at player is air
        block below player is air
        block below (block below player) is air
        stop

      set {%player%.spec.loc} to player's location
      if arg-1 is "creative":
        set {%player%.spec.mode} to 1
        set player's gamemode to creative
        send action bar "Going to the creative world!" to player
        make console execute command "/mvtp %player% creative"
        enable flight for player
      else:
        set {%player%.spec.mode} to 0
        set player's gamemode to spectator
        send action bar "Activated Spectator Mode" to player
    else:
      if {%player%.spec.loc} is not set:
        send "§cYou don't have a spec location! Ask an admin for help."
        stop

      if {%player%.spec.mode} = 0: # spectator
        send action bar "Deativated Spectator Mode" to player
      else if {%player%.spec.mode} = 1: # creative
        send action bar "Going back to survival!" to player
      
      set player's gamemode to survival
      teleport player to {%player%.spec.loc}
      disable flight for player
      delete {%player%.spec.loc}
      delete {%player%.spec.mode}


command /create:
  aliases: /cr
  trigger:
    make player execute command "/spec creative"


on world change:
  player's world is "creative"
  set player's gamemode to creative
  enable flight for player


command /dimension [<text>]:
  executable by: players
  aliases: /dim
  trigger:
    if gamemode of player is not spectator:
      send "§cYou are not in spectator mode!"
      stop
    
    if arg-1 is not set:
      send "/dim <o|n|e>%nl%/dim e §7to go to the end"
      stop
    if arg-1 is "o" or "n" or "e":
      teleport player to spawn point of "world" if arg-1 is "o"
      teleport player to spawn point of "world_nether" if arg-1 is "n"
      teleport player to spawn point of "world_the_end" if arg-1 is "e"
      send action bar "Teleporting..." to player
      wait 1 second
      set gamemode of player to spectator
    else:
      send "/dim <o|n|e>%nl%/dim e §7to go to the end"

command /spectoggletp <player=%player%>:
  aliases: /sptoggletp, /sptptoggle, /spectptoggle
  trigger:
    set {_p} to arg-1
    if player is not op:
      set {_p} to player
    if {%{_p}%.spec.enabled} is true:
      set {%{_p}%.spec.enabled} to false
      send "§cPlayers may no longer /sptp to %{_p}% §7(/sptptoggle)"
    else if {%{_p}%.spec.enabled} is false:
      set {%{_p}%.spec.enabled} to true
      send "§aPlayers may now /sptp to %{_p}% §7(/sptptoggle)"


command /spectp <player>:
  executable by: players
  aliases: /sptp
  trigger:
    if gamemode of player is not spectator:
      send "§cYou are not in spectator mode!"
      stop
    if arg-1 is player:
      send "§cThere's no point in trying to teleport to yourself."
      stop
    
    if {%arg-1%.spec.enabled} is false:
      send "§cThis player has /sptp disabled."
      stop
    teleport player to arg-1
    send action bar "Teleporting to %arg-1%..." to player

on join:
  {%player%.spec.loc} is set
  wait 10 ticks
  set player's gamemode to spectator
  send "You have been placed in spectator mode. /spec to exit"