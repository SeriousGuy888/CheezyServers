options:
  prefix: [BZAC]

  gravityConst: 0.08 # minecraft gravitational constant for living entities

  speedTestInterval: 4 # in ticks
  goodTps: 20

  warnMessage: "§7§oFaster than normal speed detected!"
  kickMessage: "%nl%§4§lKicked from CheezSurv%nl%§7Illegal movement speed!%nl%"

  infractionClearRate: 10 seconds
  rubberBandInfractions: 5 # how many infractions until rubberbanding
  maxInfractions: 10 # how many infractions until kick

  warnGliding: 78.4 # maximum possible in vanilla
  infracGliding: 80 # add infraction
  warnSprint: 8
  infracSprint: 11

every {@infractionClearRate}:
  loop all players:
    set {speed.infractions.%loop-player%} to max({speed.infractions.%loop-player%} - 1, 0)

every {@speedTestInterval} ticks:
  loop all players:
    if {speed.old_loc.%loop-player%} is not set:
      set {speed.old_loc.%loop-player%} to loop-player's location
      stop

    set {_old_x} to x-coordinate of {speed.old_loc.%loop-player%}
    set {_old_y} to y-coordinate of {speed.old_loc.%loop-player%}
    set {_old_z} to z-coordinate of {speed.old_loc.%loop-player%}

    set {_new_x} to x-coordinate of loop-player
    set {_new_y} to y-coordinate of loop-player
    set {_new_z} to z-coordinate of loop-player

    set {_x_movement} to abs({_new_x} - {_old_x})
    set {_y_movement} to abs({_new_y} - {_old_y})
    set {_z_movement} to abs({_new_z} - {_old_z})
    # set {_x_movement} to abs(x of loop-player's velocity) * 20
    # set {_y_movement} to abs((y of loop-player's velocity + {@gravityConst}) * 20)
    # set {_z_movement} to abs(z of loop-player's velocity) * 20

    set {_xz_movement} to sqrt of ({_x_movement}^2 + {_z_movement}^2) / ({@speedTestInterval} / {@goodTps})
    set {_xyz_movement} to sqrt of ({_xz_movement}^2 + {_y_movement}^2)

    loop-player's gamemode is not creative or spectator # exempt spectator and creative players
    loop-player is not riding # player is not in boat or minecart or on a horse or something
    if glider state of loop-player is true:
      set {_maxSpeedToWarn} to {@warnGliding}
      set {_maxSpeedToInfraction} to {@infracGliding}
      set {_speed} to {_xyz_movement}
    else:
      set {_maxSpeedToWarn} to {@warnSprint}
      set {_maxSpeedToInfraction} to {@infracSprint}
      set {_speed} to {_xyz_movement}
    

    if {speed.show_info.%loop-player%} = true:
      send action bar "Vel: %{_x_movement}%, %{_y_movement}%, %{_z_movement}% | XYZ/XZ: %{_xyz_movement}%, %{_xz_movement}% | Inf: %{speed.infractions.%loop-player%}%" to loop-player

    # send {@warnMessage} to loop-player if {_speed} > {_maxSpeedToWarn}
    if {_speed} > {_maxSpeedToInfraction}:
      add 1 to {speed.infractions.%loop-player%}
      set {_rubberbanded} to ""
      if {speed.infractions.%loop-player%} >= {@rubberBandInfractions}:
        teleport loop-player to {speed.old_loc.%loop-player%}
        set {_rubberbanded} to " & rubberbanded"
      send "{@prefix} %loop-player% moving too quickly at XYZ: %{_xyz_movement}%m/s; XZ: %{_xz_movement}%m/s. Added infraction, now %{speed.infractions.%loop-player%}%%{_rubberbanded}%" to console
    if {speed.infractions.%loop-player%} >= {@maxInfractions}:
      kick loop-player due to {@kickMessage}
      set {speed.infractions.%loop-player%} to 0
      stop

    set {speed.old_loc.%loop-player%} to loop-player's location


on join:
  set {speed.show_info.%player%} to false if {speed.show_info.%player%} is not set

command /togglespeedinfo:
  trigger:
    if {speed.show_info.%player%} is false:
      set {speed.show_info.%player%} to true
      send "§6now showing speed info"
    else if {speed.show_info.%player%} is true:
      set {speed.show_info.%player%} to false
      send "§6no longer showing speed info"