on right click on stair:
  set {chairs.%player%} to true if {chairs.%player%} is not set
  {chairs.%player%} is true

  block above clicked block is transparent
  "%type of clicked block%" does not contain "top"
  
  if player is not sneaking:
    if player's vehicle is not set:
      cancel event
      spawn minecart at location 0 above and 0 in front of clicked block
      set {_s} to last spawned entity
      make player ride {_s}
      set metadata "sitting" of player to location of clicked block
      set metadata "chair" of {_s} to true
 
on left click:
  metadata "chair" of clicked entity is true
  cancel event

on break:
  loop all players:
    metadata "sitting" of loop-player is location of event-block
    make loop-player dismount

on dismount:
  metadata "sitting" of player is set
  kill player's vehicle
  delete metadata "sitting" of player

on quit:
  metadata "sitting" of player is set
  make player dismount

on skript stop:
  loop all players:
    metadata "sitting" of loop-player is set
    make loop-player dismount

command /chair:
  trigger:
    set {chairs.%player%} to true if {chairs.%player%} is not set
    if {chairs.%player%} is true:
      set {chairs.%player%} to false
      send "§dToggled chairs §coff§d."
    else if {chairs.%player%} is false:
      set {chairs.%player%} to true
      send "§dToggled chairs §aon§d."