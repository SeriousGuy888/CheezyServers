options:
  genName: §eCombustion Generator
  genItem: furnace
  battName: §eBattery
  battItem: bone block
  chairName: §eElectric Chair
  chairItem: smooth quartz stairs

  batteryMaxPower: 100
  genPowerPerItem: 3



function noMoveElecComps(m: blocks) :: boolean:
  loop {_m::*}:
    loop worlds:
      return true if {power.generators::%loop-world%::*} contains loop-value-1's location
      return true if {power.batteries::%loop-world%::*} contains loop-value-1's location
      return true if {power.chairs::%loop-world%::*} contains loop-value-1's location

function consumePower(w: world, l: location, amt: number):
  loop blocks in radius 1 around {_l}: # search for adjacent batteries
    {power.batteries::%{_w}%::*} contains location of loop-block
    if {power.batteries::%{_w}%::%location of loop-block%.power} > {_amt}:
      subtract {_p} from {power.batteries::%{_w}%::%location of loop-block%.power}
      add loop-block to {_b::*}
  loop {_b::*}:
    subtract {_amt} / size of {_b::*} from {power.batteries::%{_w}%::%location of loop-value%.power}
    if {power.batteries::%{_w}%::%location of loop-value%.power} + 0 > {@batteryMaxPower}:
      set {power.batteries::%{_w}%::%location of loop-value%.power} to {@batteryMaxPower}
    show 5 portal above location of loop-value

    
command /getgen:
  trigger:
    if player is not op:
      send "§cInsufficient permissions!"
      stop
    give player {@genItem} named "{@genName}"
command /getbatt:
  trigger:
    if player is not op:
      send "§cInsufficient permissions!"
      stop
    give player {@battItem} named "{@battName}"
command /getchair:
  trigger:
    if player is not op:
      send "§cInsufficient permissions!"
      stop
    give player {@chairItem} named "{@chairName}"

on place:
  if event-block is {@genItem}:
    name of player's tool is "{@genName}"
    send "§ePlaced combustion generator!"
    add event-location to {power.generators::%event-world%::*}
  if event-block is {@battItem}:
    name of player's tool is "{@battName}"
    send "§ePlaced battery!"
    set {_start_power} to uncoloured line 1 of lore of player's tool
    if {_start_power} is set:
      set {_start_power} to {_start_power} parsed as number
    else:
      set {_start_power} to 0
    add event-location to {power.batteries::%event-world%::*}
    set {power.batteries::%event-world%::%event-location%.power} to {_start_power}
  if event-block is {@chairItem}:
    name of player's tool is "{@chairName}"
    send "§ePlaced electric chair!"
    add event-location to {power.chairs::%event-world%::*}
    set {power.chairs::%event-world%::%event-location%.active} to true

on break:
  if {power.generators::%event-world%::*} contains event-location:
    cancel event
    set event-block to air
    remove event-location from {power.generators::%event-world%::*}
    send "§eRemoved combustion generator!"
    drop 1 of {@genItem} named "{@genName}" at event-location
  if {power.batteries::%event-world%::*} contains event-location:
    cancel event
    set event-block to air
    send "§eRemoved battery!"
    drop 1 of {@battItem} named "{@battName}" with lore "§7%{power.batteries::%event-world%::%event-location%.power}%" and "§7Power Stored" at event-location
    delete {power.batteries::%event-world%::%event-location%.power}
    remove event-location from {power.batteries::%event-world%::*}
  if {power.chairs::%event-world%::*} contains event-location:
    cancel event
    set event-block to air
    send "§eRemoved electric chair!"
    drop 1 of {@chairItem} named "{@chairName}" at event-location
    delete {power.chairs::%event-world%::%event-location%.active}
    remove event-location from {power.chairs::%event-world%::*}

on smelt:
  {power.generators::%event-world%::*} contains event-location
  wait 1 tick
  set result of event-block to air
  consumePower(event-world, event-location, -{@genPowerPerItem})

every second:
  loop worlds:
    loop {power.chairs::%loop-world%::*}:
      {power.chairs::%loop-world%::%loop-value-2%.active} is true
      set {_p} to 4
      loop blocks in radius 1 around loop-value-2:
        {power.batteries::%loop-world%::*} contains location of loop-block
        if {power.batteries::%loop-world%::%location of loop-block%.power} > {_p}:
          consumePower(loop-world, location of loop-block, {_p})
          loop entities in radius 0.75 around block above loop-value-2:
            if loop-entity is player:
              set metadata "power_electric_chair" of loop-entity to true
            damage loop-entity by 7.5
            wait 5 ticks
            set metadata "power_electric_chair" of loop-entity to false

on death of player:
  metadata "power_electric_chair" of player is true
  set death message to "%player% was electrocuted"

on right click on {@battItem}:
  player is not sneaking
  {power.batteries::%event-world%::*} contains event-location
  cancel event
  if {power.batteries::%event-world%::%event-location%.power} > {@batteryMaxPower}:
    set {power.batteries::%event-world%::%event-location%.power} to {@batteryMaxPower}
  send "§e§lBattery Power: §e%{power.batteries::%event-world%::%event-location%.power}%"
on right click on {@chairItem}:
  player is not sneaking
  {power.chairs::%event-world%::*} contains event-location
  cancel event
  if {power.chairs::%event-world%::%event-location%.active} is false:
    set {power.chairs::%event-world%::%event-location%.active} to true
    send "Enabled"
  else:
    set {power.chairs::%event-world%::%event-location%.active} to false
    send "Disabled"

on piston extend:
  cancel event if noMoveElecComps(moved blocks) is true
on piston retract:
  cancel event if noMoveElecComps(moved blocks) is true