options:
  shieldComboTimer: 3.25 seconds
  shieldComboSound: "block.anvil.place" at volume 0.1
  shieldComboDamageFactor: 0.5
  shieldComboMax: 16

function clearCombo(p: player):
  delete {combat.shield_combo.%{_p}%.time}
  set {combat.shield_combo.%{_p}%.count} to 0
  delete {combat.shield_combo.%{_p}%.dmg}

every 2.5 seconds:
  loop all players:
    if difference between now and {combat.shield_combo.%loop-player%.time} > {@shieldComboTimer}:
      clearCombo(loop-player)
      stop

every 5 ticks:
  loop all players:
    {combat.shield_combo.%loop-player%.time} is set
    set {_combo_time} to difference between now and {combat.shield_combo.%loop-player%.time}

    {_combo_time} < {@shieldComboTimer}

    set {_count} to {combat.shield_combo.%loop-player%.count}
    set {_timer} to difference between {@shieldComboTimer} and {_combo_time}
    set {_damage_bonus} to {_count} * {@shieldComboDamageFactor}
    set {_damage_dealt} to {combat.shield_combo.%loop-player%.dmg}

    set {_actionbar} to "§6§lShield Combo x%{_count}%§7: §e%{_timer}% §4(§c+%{_damage_bonus}% DMG§4) §7|§c %{_damage_dealt} otherwise 0%HP §4Dealt"
    send action bar {_actionbar} to loop-player

on damage:
  attacker is set
  victim is player
  if victim is blocking:
    damage cause = attack
    if final damage = 0:
      set {combat.shield_combo.%victim%.time} to now
      set {combat.shield_combo.%victim%.count} to 0 if {combat.shield_combo.%victim%.count} is not set
      add 1 to {combat.shield_combo.%victim%.count} if {combat.shield_combo.%victim%.count} < {@shieldComboMax}
      play sound {@shieldComboSound} to attacker
    # if final damage > 0:
    #   send action bar "damaged while using shield (s)" to victim
  else if victim is not blocking:
    damage cause != thorns
    {combat.shield_combo.%victim%.time} is set
    clearCombo(victim)
    set {combat.msg.%victim%} to true if {combat.msg.%victim%} is not set
    send "§cCombo cancelled from taking damage! §7(/togglecombatmessage)" to victim if {combat.msg.%victim%} is true

on damage:
  attacker is player
  damage cause != thorns
  difference between now and {combat.shield_combo.%attacker%.time} < {@shieldComboTimer}
  if final damage > 0:
    set {combat.shield_combo.%attacker%.time} to now
    set {combat.shield_combo.%attacker%.count} to 0 if {combat.shield_combo.%attacker%.count} is not set
    add 1 to {combat.shield_combo.%attacker%.count} if {combat.shield_combo.%attacker%.count} < {@shieldComboMax}
    play sound {@shieldComboSound} to attacker
    add ({combat.shield_combo.%attacker%.count} * {@shieldComboDamageFactor}) / 2 to damage
    set {combat.shield_combo.%attacker%.dmg} to final damage

command /togglecombatmessage:
  aliases: /togglecombatmsg
  trigger:
    set {combat.msg.%player%} to true if {combat.msg.%player%} is not set
    if {combat.msg.%player%} is false:
      set {combat.msg.%player%} to true
      send "§aToggled combat message on."
    else if {combat.msg.%player%} is true:
      set {combat.msg.%player%} to false
      send "§cToggled combat message off."