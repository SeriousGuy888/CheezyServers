options:
  comboTimer: 3.25 seconds
  comboSound: "block.anvil.place" at volume 0.1
  comboDamageFactor: 0.25
  comboMax: 32
  comboBasicMin: 3
  maxDistance: 6

function clearCombo(p: player):
  delete {combat.combo.%{_p}%.time}
  set {combat.combo.%{_p}%.count} to 0
  delete {combat.combo.%{_p}%.dmg}
  delete {combat.combo.%{_p}%.basic_buildup}

every 2.5 seconds:
  loop all players:
    if difference between now and {combat.combo.%loop-player%.time} > {@comboTimer}:
      clearCombo(loop-player)
      stop

every 5 ticks:
  loop all players:
    {combat.combo.%loop-player%.time} is set
    set {_combo_time} to difference between now and {combat.combo.%loop-player%.time}

    {_combo_time} < {@comboTimer}

    set {_count} to {combat.combo.%loop-player%.count}
    set {_timer} to difference between {@comboTimer} and {_combo_time}
    set {_damage_bonus} to {_count} * {@comboDamageFactor}
    set {_damage_dealt} to {combat.combo.%loop-player%.dmg}

    set {_actionbar} to "§6§lCombo x%{_count}%§7: §e%{_timer}% §4(§c+%{_damage_bonus}% DMG§4) §7|§c %{_damage_dealt} otherwise 0%HP §4Dealt"
    send action bar {_actionbar} to loop-player

on damage:
  attacker is set
  victim is player
  if victim is blocking:
    damage cause = attack
    if final damage = 0:
      set {combat.combo.%victim%.time} to now
      set {combat.combo.%victim%.count} to 0 if {combat.combo.%victim%.count} is not set
      add 1 to {combat.combo.%victim%.count} if {combat.combo.%victim%.count} < {@comboMax}
      play sound {@comboSound} to attacker
    # if final damage > 0:
    #   send action bar "damaged while using shield (s)" to victim
  else if victim is not blocking:
    damage cause != thorns
    {combat.combo.%victim%.time} is set
    clearCombo(victim)
    set {combat.msg.%victim%} to true if {combat.msg.%victim%} is not set
    send "§cCombo cancelled from taking damage! §7(/togglecombatmessage)" to victim if {combat.msg.%victim%} is true

on damage:
  attacker is player
  damage cause != thorns
  final damage > 0

  if difference between now and {combat.combo.%attacker%.time} < {@comboTimer}:
    distance between location of attacker and location of victim < {@maxDistance}
    set {combat.combo.%attacker%.time} to now
    set {combat.combo.%attacker%.count} to 0 if {combat.combo.%attacker%.count} is not set
    add 1 to {combat.combo.%attacker%.count} if {combat.combo.%attacker%.count} < {@comboMax}
    play sound {@comboSound} to attacker
    add ({combat.combo.%attacker%.count} * {@comboDamageFactor}) / 2 to damage
    set {combat.combo.%attacker%.dmg} to final damage
  
  if {combat.combo.%attacker%.victim} = victim:
    set {combat.combo.%attacker%.time} to now
    if difference between now and {combat.combo.%attacker%.time} < {@comboTimer}:
      add 1 to {combat.combo.%attacker%.basic_buildup}

      if {combat.combo.%attacker%.basic_buildup} >= {@comboBasicMin}:
        add 1 to {combat.combo.%attacker%.count} if {combat.combo.%victim%.count} < {@comboMax}
  else:
    set {combat.combo.%attacker%.victim} to victim
    set {combat.combo.%attacker%.basic_buildup} to 0

command /togglecombatmessage:
  aliases: /togglecombatmsg
  trigger:
    set {combat.msg.%player%} to true if {combat.msg.%player%} is not set
    if {combat.msg.%player%} is false:
      set {combat.msg.%player%} to true
      send "§aToggled combat message on."
    else if {combat.msg.%player%} is true:
      set {combat.msg.%player%} to false
      send "§cToggled combat message off."