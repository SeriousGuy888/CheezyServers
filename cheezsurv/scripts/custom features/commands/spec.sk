command /spec:
  executable by: players
  aliases: /sp
  trigger:
    set {_success} to true
    if player's gamemode is survival:
      set {%player%.spec.loc} to player's location
      set player's gamemode to spectator
      send action bar "Activated Spectator Mode" to player
    else if player's gamemode is spectator:
      if {%player%.spec.loc} is set:
        set player's gamemode to survival
        teleport player to {%player%.spec.loc}
        delete {%player%.spec.loc}
        send action bar "Deativated Spectator Mode" to player
      else if {%player%.spec.loc} is not set:
        send "§cYou are not in /spec spectator mode."
        set {_success} to false
    if {_success} is true:
      loop all players:
        {subspec.%loop-player%} is true
        send "%player% used /sp" to loop-player

command /dimension [<text>]:
  executable by: players
  aliases: /dim
  trigger:
    if gamemode of player is not spectator:
      send "§cYou are not in spectator mode!"
      stop
    
    if arg-1 is not set:
      send "/dim <o|n|e>%nl%/dim e §7to go to the end"
      stop
    if arg-1 is "o" or "n" or "e":
      teleport player to spawn point of "world" if arg-1 is "o"
      teleport player to spawn point of "world_nether" if arg-1 is "n"
      teleport player to spawn point of "world_the_end" if arg-1 is "e"
      send action bar "Teleporting..." to player
    else:
      send "/dim <o|n|e>%nl%/dim e §7to go to the end"

command /spectp <player>:
  executable by: players
  aliases: /sptp
  trigger:
    if gamemode of player is not spectator:
      send "§cYou are not in spectator mode!"
      stop
    if arg-1 is player:
      send "§cThere's no point in trying to teleport to yourself."
      stop
    
    teleport player to arg-1
    send action bar "Teleporting to %arg-1%..." to player
    
    loop all players:
      {subspec.%loop-player%} is true
      send "%player% used /sptp %arg-1%" to loop-player
    
command /subscribespec:
  executable by: players
  aliases: /subsp, /subscribesp, /subspec
  trigger:
    set {subspec.%player%} to false if {subspec.%player%} is not set
    if {subspec.%player%} is false:
      set {subspec.%player%} to true
      send "§3Subscribed. You will be notified when people use /sp or /sptp"
    else if {subspec.%player%} is true:
      set {subspec.%player%} to false
      send "§3Unsubscribed."

on join:
  {%player%.spec.loc} is set
  wait 10 ticks
  set player's gamemode to spectator
  send "You have been placed in spectator mode. /spec to exit"