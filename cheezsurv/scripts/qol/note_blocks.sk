function getNoteNumber(str: text) :: number:
  set {_num} to 0

  if {_str} contains ".":
    return 0
  if {_str} contains "G":
    set {_num} to 1
  else if {_str} contains "A":
    set {_num} to 3
  else if {_str} contains "B":
    set {_num} to 5
  else if {_str} contains "C":
    set {_num} to 6
  else if {_str} contains "D":
    set {_num} to 8
  else if {_str} contains "E":
    set {_num} to 10
  else if {_str} contains "F":
    set {_num} to 11

  if {_str} contains "##":
    add 1 to {_num}
  if {_str} contains "^":
    add 12 to {_num}
  
  return {_num}


function setNote(p: player, b: block, note: number):
  if {_note} is not set:
    make {_p} execute command "/noteblock"
    stop
  set {_x} to floor(x-position of {_b})
  set {_y} to floor(y-position of {_b})
  set {_z} to floor(z-position of {_b})
  make console execute command "execute at %{_p}% run setblock %{_x}% %{_y}% %{_z}% note_block[note=%{_note}%]"
  send "§aSet note of note block to %{_note}%§a. §7§o(/nb)" to {_p}


command /noteblock [<text>]:
  aliases: /nb
  trigger:
    if arg-1 is not set:
      send "§2§lSyntax%nl%"
      send "§a/nb (<Note>[##][^]|x)"
      send "§a##: Makes note sharp"
      send "§a^: Raise note by octave%nl%"
      send "§a/nb C     -  C"
      send "§a/nb F##   -  F sharp"
      send "§a/nb E     -  E, raise octave"
      send "§a/nb A##^  -  B flat, raise octave"
      send "§a/nb .     -  Default F sharp%nl%"
      send "§a/nb x     -  Cancel your note selection"
      stop
    if arg-1 contains "x":
      delete metadata "nb_note" of player
      send "§cYour note selection has been cancelled."
      stop
    set metadata "nb_note" of player to arg-1
    send "§aYour note is set to %getNoteNumber(arg-1)%§a. Sneak right click on note blocks with an empty hand to set the note."


on right click on note block:
  player has permission "verified"
  player is sneaking
  player's tool is air
  cancel event
  setNote(player, event-block, getNoteNumber(metadata "nb_note" of player))