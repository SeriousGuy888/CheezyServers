options:
  comboSound: "block.anvil.place" at volume 0.1
  comboTimer: 3.25 seconds
  comboDamageFactor: 0.25
  comboMax: 32
  comboBasicMin: 4
  maxDistance: 6
  excludedMobs: ender dragon, ender crystal

  shieldChargeSound: "item.shield.block" at volume 1
  shieldChargeEffective: 0.67 seconds
  shieldChargeRadius: 1.5
  shieldChargeCooldown: 10 seconds
  shieldChargeHungerCost: 3
  shieldChargeSaturationCost: 5
  shieldChargeDamage: 5
  shieldChargeForce: 2
  shieldChargeMaxYVel: 0.05

function clearCombo(p: player):
  delete {combat.combo.%{_p}%.time}
  set {combat.combo.%{_p}%.count} to 0
  delete {combat.combo.%{_p}%.dmg}
  delete {combat.combo.%{_p}%.basic_buildup}

every 2.5 seconds:
  loop all players:
    if difference between now and {combat.combo.%loop-player%.time} > {@comboTimer}:
      clearCombo(loop-player)
      stop

on damage:
  attacker is set
  victim is player
  if victim is blocking:
    damage cause = attack
    if final damage = 0:
      set {combat.combo.%victim%.time} to now
      set {combat.combo.%victim%.count} to 0 if {combat.combo.%victim%.count} is not set
      add 1 to {combat.combo.%victim%.count} if {combat.combo.%victim%.count} < {@comboMax}
      play sound {@comboSound} to attacker
    # if final damage > 0:
    #   send action bar "damaged while using shield (s)" to victim
  else if victim is not blocking:
    damage cause != thorns
    {combat.combo.%victim%.time} is set
    clearCombo(victim)
    set {combat.msg.%victim%} to true if {combat.msg.%victim%} is not set
    send "§cCombo cancelled from taking damage! §7(/togglecombatmessage)" to victim if {combat.msg.%victim%} is true

on damage:
  attacker is player
  damage cause != thorns
  final damage > 0
  {@excludedMobs} does not contain victim

  if difference between now and {combat.combo.%attacker%.time} < {@comboTimer}:
    victim != attacker
    distance between location of attacker and location of victim < {@maxDistance}
    set {combat.combo.%attacker%.time} to now
    set {combat.combo.%attacker%.count} to 0 if {combat.combo.%attacker%.count} is not set
    add 1 to {combat.combo.%attacker%.count} if {combat.combo.%attacker%.count} < {@comboMax}
    play sound {@comboSound} to attacker
    add ({combat.combo.%attacker%.count} * {@comboDamageFactor}) / 2 to damage
    set {combat.combo.%attacker%.dmg} to final damage
  
  if {combat.combo.%attacker%.victim} = victim:
    set {combat.combo.%attacker%.time} to now
    if difference between now and {combat.combo.%attacker%.time} < {@comboTimer}:
      add 1 to {combat.combo.%attacker%.basic_buildup}

      if {combat.combo.%attacker%.basic_buildup} >= {@comboBasicMin}:
        add 1 to {combat.combo.%attacker%.count} if {combat.combo.%victim%.count} < {@comboMax}
  else:
    set {combat.combo.%attacker%.victim} to victim
    set {combat.combo.%attacker%.basic_buildup} to 0

on left click:
  player's tool is shield
  player's gamemode is not spectator
  player's facing is not up or down

  if player's hunger <= 3:
    player's gamemode is not creative
    send "§cInsufficient hunger! §7(/togglecombatmessage)" if {combat.msg.%player%} is true
    stop
  if difference between now and {combat.shield_charge.%player%.time} < {@shieldChargeCooldown}:
    send "§6Shield charge cooldown: §e%difference between {@shieldChargeCooldown} and (difference between now and {combat.shield_charge.%player%.time})% §7(/togglecombatmessage)" if {combat.msg.%player%} is true
    stop

  if player's gamemode is not creative:
    subtract {@shieldChargeHungerCost} / 2 from player's hunger
    subtract {@shieldChargeSaturationCost} from player's saturation
  set player's saturation to (player's hunger * 2) if (player's hunger * 2) < player's saturation
  push player forward with force {@shieldChargeForce}
  set y of player's velocity to min(y of player's velocity, {@shieldChargeMaxYVel})
  send "§aShield charge §7(/togglecombatmessage)" if {combat.msg.%player%} is true
  set {combat.shield_charge.%player%.time} to now
  set metadata "shield_charging" of player to true

every tick:
  loop all players:
    metadata "shield_charging" of loop-player is true
    if difference between now and {combat.shield_charge.%loop-player%.time} > {@shieldChargeEffective}:
      clear metadata "shield_charging" of loop-player
      loop all entities:
        clear metadata "shield_charge_damaged_by_%loop-player%" of loop-entity-2
      stop
    loop all entities in radius {@shieldChargeRadius} around loop-player:
      loop-entity-2 is not loop-player
      metadata "shield_charge_damaged_by_%loop-player%" of loop-entity-2 is not true
      make loop-player damage loop-entity-2 by {@shieldChargeDamage}
      play sound {@shieldChargeSound} to loop-player
      set metadata "shield_charge_damaged_by_%loop-player%" of loop-entity-2 to true



every 5 ticks:
  loop all players:
    if loop-player's tool is shield:
      if difference between now and {combat.shield_charge.%loop-player%.time} < {@shieldChargeCooldown}:
        send action bar "§5Shield Charge Cooldown: §d%difference between {@shieldChargeCooldown} and (difference between now and {combat.shield_charge.%loop-player%.time})%" to loop-player
      else:
        send action bar "§5Shield Charge Ready" to loop-player
    else:
      {combat.combo.%loop-player%.time} is set
      set {_combo_time} to difference between now and {combat.combo.%loop-player%.time}
      {_combo_time} < {@comboTimer}
      set {_count} to {combat.combo.%loop-player%.count}
      {_count} >= 1

      set {_timer} to difference between {@comboTimer} and {_combo_time}
      set {_damage_bonus} to {_count} * {@comboDamageFactor}
      set {_damage_dealt} to {combat.combo.%loop-player%.dmg}

      set {_actionbar} to "§6§lCombo x%{_count}%§7: §e%{_timer}% §4(§c+%{_damage_bonus}% DMG§4) §7|§c %{_damage_dealt} otherwise 0%HP §4Dealt"
      send action bar {_actionbar} to loop-player



on join:
  set {combat.msg.%player%} to true if {combat.msg.%player%} is not set

command /togglecombatmessage:
  aliases: /togglecombatmsg
  trigger:
    set {combat.msg.%player%} to true if {combat.msg.%player%} is not set
    if {combat.msg.%player%} is false:
      set {combat.msg.%player%} to true
      send "§aToggled combat message on."
    else if {combat.msg.%player%} is true:
      set {combat.msg.%player%} to false
      send "§cToggled combat message off."