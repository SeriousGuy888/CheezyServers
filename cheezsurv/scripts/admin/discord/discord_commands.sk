options:
  prefixes: "s2!", "%mention tag of event-bot% "
  
discord command help:
  prefixes: {@prefixes}
  aliases: ?
  trigger:
    set {_msg} to a message builder
    append line "**CheezSurv2 Bot Commands**"
    append line "Prefix: `s2!` or mention the bot"
    append line ""
    append line "`list` - See online players"
    append line "`stats <Player>` - See the stats of a specified player"
    append line "`bal <Player>` - See the bank balance of a specified player"
    append line "`stocklist <Player>` - See the stocks that a player owns"
    append line "`stocklookup <Ticker>` - Query the company that a ticker represents"

    reply with {_msg}


discord command list:
  prefixes: {@prefixes}
  aliases: online
  trigger:
    set {_is.are} to "are"
    set {_is.are} to "is" if number of all players is 1

    set {_player.players} to "players"
    set {_player.players} to "player" if number of all players is 1

    set {_msg} to a message builder
    append line "**CHEEZSURV2 ONLINE MEMBERS**"
    append line "There %{_is.are}% `%number of all players%` %{_player.players}% online out of a maximum of `%max players count%`."
    append line "----------"
    append line "The members online are:"
    loop all players:
      set {_curr.player} to "%loop-player%"
      set {_curr.player} to "%{_curr.player}% (%{%loop-player%.nick}%)" if {%loop-player%.nick} is set
      set {_curr.player} to "%{_curr.player}% [AFK]" if {%loop-player%.afk} is true
      append line "`%{_curr.player}%`"
    reply with {_msg}
    
discord command stats [<OfflinePlayer>]:
  prefixes: {@prefixes}
  aliases: stat
  trigger:
    if arg-1 is not set:
      reply with "Syntax: s2!stats <Player>"
      stop
    if {stats.%arg-1%.deaths} is not set:
      reply with "no player found by that name. try spelling it right next time. im not helping you search for someone with similar usernames. it wont work because billzo already tried to add that feature and it didnt work. spell the username correctly, idot."
      stop
    set {_msg} to a message builder
    append line "**Stats of %arg-1%**"
    append line "ðŸ’€ Deaths: %{stats.%arg-1%.deaths}%"
    append line "ðŸ”ª Murders: %{stats.%arg-1%.murders}%"
    append line "âž— K/D: %{stats.%arg-1%.murders}/{stats.%arg-1%.deaths}%"

    reply with {_msg}

discord command balance [<OfflinePlayer>]:
  prefixes: {@prefixes}
  aliases: bal
  trigger:
    if arg-1 is not set:
      reply with "Syntax: s2!balance <Player>"
      stop
    if {bank.balance.%arg-1%} is not set:
      reply with "No player found by that name. Try fixing your spelling."
      stop

    reply with "**Bank Balance of %arg-1%**%nl%%{bank.balance.%arg-1%}% coins"


discord command stocklookup [<text>]:
  prefixes: {@prefixes}
  trigger:
    if arg-1 is not set:
      reply with "s2!stocklookup <Ticker Symbol>"
      stop
    set {_ticker} to arg-1 in upper case
    if {stocks.ticker.%{_ticker}%} is set:
      reply with "**%{_ticker}%**: *%{stocks.ticker.%{_ticker}%}%* | %{stocks.value.%{_ticker}%}% coins%nl%**Registered owners**: %{stocks.owners.%{_ticker}%::*} otherwise ""[NONE]""%"
    else:
      reply with "%{_ticker}% is not a registered ticker symbol."

discord command stocklist [<OfflinePlayer>]:
  prefixes: {@prefixes}
  trigger:
    if arg-1 is not set:
      reply with "s2!stocklist <Player>"
      stop
    set {_m} to "**Stocks of %arg-1%**"
    set {_net_worth} to 0
    loop {stocks.registered::*}:
      set {stocks.ownership.%arg-1%.%{stocks.registered::%loop-index%}%} to 0 if {stocks.ownership.%arg-1%.%{stocks.registered::%loop-index%}%} is not set
      set {_m} to "%{_m}%%nl%- __%{stocks.registered::%loop-index%}%__: %{stocks.value.%{stocks.registered::%loop-index%}%}% coins%nl%  Owned: %{stocks.ownership.%arg-1%.%{stocks.registered::%loop-index%}%}% shares."
      add {stocks.value.%{stocks.registered::%loop-index%}%} * {stocks.ownership.%arg-1%.%{stocks.registered::%loop-index%}%} to {_net_worth}
    set {_m} to "%{_m}%%nl%%nl%**NET WORTH IN STOCKS**%nl%%{_net_worth}% coins"
    reply with {_m}

discord command stockbalance [<text>]:
  prefixes: {@prefixes}
  aliases: stockbal
  trigger:
    if arg-1 is not set:
      reply with "s2!stockbalance <ticker>"
      stop
    set {_ticker} to arg-1 in upper case
    if {stocks.registered::*} does not contain {_ticker}:
      reply with "Stock not registered!"
    
    reply with "%{_ticker}%: %{stocks.balance.%{_ticker}%}%"

discord command tps:
  prefixes: {@prefixes}
  trigger:
    set {_tps} to tps from last minute
    reply with "**Server TPS:** %{_tps}%"

discord command ping [<player>]:
  prefixes: {@prefixes}
  trigger:
    if arg-1 is not set:
      reply with "s2!ping <player>"
      stop
    set {_ping} to arg-1's ping
    reply with "**Ping of %arg-1%:** %{_ping}%ms"