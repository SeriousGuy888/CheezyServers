options:
  tagBackCooldown: 3 seconds

command /makeit <offlineplayer>:
  trigger:
    player is op
    set {tag_game.it} to arg-1
    send "%arg-1% is now it!"

command /whoisit:
  trigger:
    send "%{tag_game.it}% is currently IT!"

on join:
  if {tag_game.it} is player:
    send "You are currently IT! Tag someone to make them it!"
  else:
    make player execute command "/whoisit"

on damage:
  damage cause is not thorns
  attacker is player
  victim is player

  attacker is not victim


  if victim is {tag_game.last_tag.tagger}:
    set {_cooldown} to difference between now and {tag_game.last_tag.time}
    if {_cooldown} < {@tagBackCooldown}:
      send "Tagback Cooldown: %{_cooldown}% / {@tagBackCooldown}" to (attacker and victim)
      stop


  {tag_game.it} is attacker

  add 1 to {stats.%attacker%.tagged}
  add 1 to {stats.%victim%.tagged_by}

  set {tag_game.last_tag.time} to now
  set {tag_game.last_tag.tagger} to attacker

  set {tag_game.it} to victim
  loop all players:
    if loop-player is victim:
      send "Tag from %attacker%! You're it!" to loop-player
    else:
      send "%attacker% tagged %victim%!" to loop-player
  make console execute command "/sendtodiscordchat :point_up_2: **%attacker% tagged %victim%.**"