options:
  prefixes: "s5!", "%mention tag of event-bot% "
  codePrefix: "surv5-"
  codeLength: 6

on skript unload:
  send "Server shutting down; all temporary Discord link codes are being removed." to console
  loop {discord_link.players.linking::*}:
    delete {discord_link.code.%loop-value%}
    delete {discord_link.code.%loop-value%.expires}
  delete {discord_link.players.linking::*}
  delete {discord_link.codes_in_use::*}

command /link:
  aliases: /linkaccount
  trigger:
    if {discord_link.link.player.%player%} is set:
      send "§cThis account is already linked!"
      stop

    set {_code} to {@codePrefix}
    loop {@codeLength} times:
      set {_code} to "%{_code}%%random integer between 0 and 9%"
    if {discord_link.codes_in_use::*} contains {_code}:
      send "§cError with generating code. Please try again. Please contact admins if problem persists."
      stop


    if {discord_link.players.linking::*} contains player:
      send "This Minecraft account was already in the process of linking. The previous linking code has been invalidated."

    add player to {discord_link.players.linking::*}
    add {_code} to {discord_link.codes_in_use::*}
    set {discord_link.code.%player%} to {_code}
    set {discord_link.player.%{_code}%} to player
    set {discord_link.code.%player%.created} to now

    send new line
    send "§7Use the Discord command §bs5!link %{_code}%§7 to complete linkage. Your code will expire in 15 minutes or when the server goes offline. Following the linkage, CheezSurv Discord commands will be able to default to your username."
    send new line

command /unlink:
  trigger:
    if {discord_link.link.player.%player%} is not set:
      send "§cThis account is not linked!"
      stop
    
    delete {discord_link.link.discord.%{discord_link.link.player.%player%}%}
    delete {discord_link.link.player.%player%}

    send "Successfully unlinked your Discord ID with Minecraft."

discord command link [<text>]:
  prefixes: {@prefixes}
  trigger:
    if arg-1 is not set:
      reply with "link <Linking Code>%nl%`Obtain a linking code by running /link in game`"
      stop

    set {_code} to arg-1
    if {discord_link.codes_in_use::*} does not contain {_code}:
      reply with "The linking code provided is invalid."
    else:
      set {_p} to {discord_link.player.%{_code}%}
      if difference between {discord_link.code.%{_p}%.created} and now is more than 15 minutes:
        reply with "This code has already expired! Please obtain a new one."
        remove {_code} from {discord_link.codes_in_use::*}
        delete {discord_link.code.%{discord_link.player.%{_code}%}%}
        delete {discord_link.player.%{_code}%}
      else:
        set {_msg} to a message builder
        append line "**Linking Info**"
        append line "---"
        append line "Minecraft Account: `%{discord_link.player.%{_code}%}%`"
        append line "Discord Account: `%discord id of author of event-message%`"
        append line "Linking Code: `%{discord_link.code.%{discord_link.player.%{_code}%}%}%`"
        append line "Linking Code Creation Timestamp: `%{discord_link.code.%{discord_link.player.%{_code}%}%.created}%`"
        reply with {_msg}

        set {discord_link.link.player.%{discord_link.player.%{_code}%}%} to discord id of author of event-message
        set {discord_link.link.discord.%discord id of author of event-message%} to {discord_link.player.%{_code}%}

        reply with "Successfully linked Minecraft Account `%{discord_link.link.discord.%discord id of author of event-message%}%` to Discord ID `%{discord_link.link.player.%{discord_link.player.%{_code}%}%}%`"

        remove {_code} from {discord_link.codes_in_use::*}
        delete {discord_link.code.%{discord_link.player.%{_code}%}%}
        delete {discord_link.player.%{_code}%}


discord command unlink:
  prefixes: {@prefixes}
  trigger:
    if {discord_link.link.discord.%discord id of author of event-message%} is not set:
      reply with "This Discord ID is not linked!"
      stop
    
    set {_player} to {discord_link.link.player.%{discord_link.link.discord.%discord id of author of event-message%}%}

    delete {discord_link.link.player.%{discord_link.link.discord.%discord id of author of event-message%}%}
    delete {discord_link.link.discord.%discord id of author of event-message%}

    reply with "Successfully unlinked your Discord ID with Minecraft."
    send "Your Discord ID was unlinked through Discord." to {_player}