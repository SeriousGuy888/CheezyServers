options:
  prefixes: "s3!", "%mention tag of event-bot% "
  linkDefaultedMsg: "`Defaulted to looking up your player data as you have your Discord ID linked.`%nl%"
  specifyPlayer: "Please specify a player name. If you wish for the command to default to your info, link your Discord ID with `/link` in game."
  playerNotFound: "No player found by that name. Try fixing your spelling."

discord command help:
  prefixes: {@prefixes}
  aliases: ?
  trigger:
    reply with "duck you im not keeping an up to date list of commands. use <##702392225174388786>"


discord command list:
  prefixes: {@prefixes}
  aliases: online
  trigger:
    set {_is.are} to "are"
    set {_is.are} to "is" if number of all players is 1

    set {_player.players} to "players"
    set {_player.players} to "player" if number of all players is 1

    set {_msg} to a message builder
    append line "**CHEEZSURV ONLINE MEMBERS**"
    append line "There %{_is.are}% `%number of all players%` %{_player.players}% online out of a maximum of `%max players count%`."
    append line "----------"
    append line "The members online are:"
    loop all players:
      set {_curr.player} to "%loop-player%"
      set {_curr.player} to "%{_curr.player}% (%{%loop-player%.nick}%)" if {%loop-player%.nick} is set
      set {_curr.player} to "%{_curr.player}% [AFK]" if {%loop-player%.afk} is true
      append line "`%{_curr.player}%`"
    reply with {_msg}
    
discord command stats [<OfflinePlayer>]:
  prefixes: {@prefixes}
  aliases: stat
  trigger:
    set {_p} to arg-1
    set {_link_defaulted} to false

    if arg-1 is not set:
      if {discord_link.link.discord.%discord id of author of event-message%} is set:
        set {_p} to {discord_link.link.discord.%discord id of author of event-message%}
        set {_link_defaulted} to true
      else:
        reply with {@specifyPlayer}
        stop
    
    if {stats.%{_p}%.deaths} is not set:
      reply with {@playerNotFound}
      stop
    set {_msg} to a message builder
    append line {@linkDefaultedMsg} if {_link_defaulted} is true
    append line "**Stats of %{_p}%**"
    append line "ðŸ’€ Deaths: %{stats.%{_p}%.deaths}%"
    append line "ðŸ”ª Murders: %{stats.%{_p}%.murders}%"
    append line "âž— K/D: %{stats.%{_p}%.murders}/{stats.%{_p}%.deaths}%"
    append line "âŒ› Playtime: %timeToText({playtime.%{_p}%})%"
    append line "ðŸ“… Login Streak: %{login_streak::%{_p}%.streak}%"
    append line "  "
    # append line "*\*Playtime and stat tracking was reset May 27th, 2020*"
    append line "*\*Playtime only recalculates every time the player leaves*"
    append line "*\*While the default Minecraft playtime statistic exists, we do not use it as it requires the player to be online to access.*"
    reply with {_msg}

discord command balance [<OfflinePlayer>]:
  prefixes: {@prefixes}
  aliases: bal
  trigger:
    set {_p} to arg-1
    set {_link_defaulted} to false

    if arg-1 is not set:
      if {discord_link.link.discord.%discord id of author of event-message%} is set:
        set {_p} to {discord_link.link.discord.%discord id of author of event-message%}
        set {_link_defaulted} to true
      else:
        reply with {@specifyPlayer}
        stop
    
    if {bank.balance::%{_p}%} is not set:
      reply with {@playerNotFound}
      stop

    set {_msg} to a message builder
    append line {@linkDefaultedMsg} if {_link_defaulted} is true
    append line "**Bank Balance of %{_p}%**%nl%%{bank.balance::%{_p}%}% coins"

    reply with {_msg}

discord command balancetop [<OfflinePlayer>]:
  prefixes: {@prefixes}
  aliases: baltop
  trigger:
    set {_msg} to a message builder
    append line {@linkDefaultedMsg} if {_link_defaulted} is true
    append line "**__CheezSurv Richest Players__**"
    append line "*Values are rounded. Use `bal` for exact values. (Totally not because billzo doesn't know how to sort numbers.)*%nl%"

    loop {bank.balance::*}:
      add 1 to {_size}
      if {_low_to_high_list::%loop-value%} is not set:
        set {_low_to_high_list::%loop-value%} to loop-index
      else:
        set {_n} to 0
        loop {_size} times:
          add 1 to {_n}
          if {_low_to_high_list::%loop-value-1%.%{_n}%} is not set:
            set {_low_to_high_list::%loop-value-1%.%{_n}%} to loop-index
          stop loop
    wait 1 tick
    set {_n} to size of {_low_to_high_list::*}
    loop {_low_to_high_list::*}:
      set {_high_to_low_list::%{_n}%} to loop-value
      subtract 1 from {_n}
    wait 1 tick
    set {_total_players} to 0
    loop {_high_to_low_list::*}:
      add 1 to {_ranking}
      {bank.balance::%loop-value%} > 0

      set {_player} to "%loop-value%" parsed as offline player
      set {_player} to "%{_player}%"
      replace all "_" with "\_" in {_player}
      replace all "*" with "\*" in {_player}

      append line "`%{_ranking}%.` %{_player}% â†’ %goodifyNumber({bank.balance::%loop-value%})% coins"
      if {_ranking} = 25:
        stop loop
      add 1 to {_total_players}
    
    reply with {_msg}


discord command stocklookup [<text>]:
  prefixes: {@prefixes}
  trigger:
    if arg-1 is not set:
      reply with "s2!stocklookup <Ticker Symbol>"
      stop
    set {_ticker} to arg-1 in upper case
    if {stocks.ticker.%{_ticker}%} is set:
      reply with "**%{_ticker}%**: *%{stocks.ticker.%{_ticker}%}%* | %{stocks.value.%{_ticker}%}% coins%nl%**Registered owners**: %{stocks.owners.%{_ticker}%::*} otherwise ""[NONE]""%"
    else:
      reply with "%{_ticker}% is not a registered ticker symbol."

discord command stocklist [<OfflinePlayer>]:
  prefixes: {@prefixes}
  trigger:
    set {_p} to arg-1
    set {_link_defaulted} to false

    if arg-1 is not set:
      if {discord_link.link.discord.%discord id of author of event-message%} is set:
        set {_p} to {discord_link.link.discord.%discord id of author of event-message%}
        set {_link_defaulted} to true
      else:
        reply with {@specifyPlayer}
        stop
    
    set {_m} to "**Stocks of %{_p}%**"
    set {_net_worth} to 0
    loop {stocks.registered::*}:
      set {stocks.ownership.%{_p}%.%{stocks.registered::%loop-index%}%} to 0 if {stocks.ownership.%{_p}%.%{stocks.registered::%loop-index%}%} is not set
      set {_m} to "%{_m}%%nl%- __%{stocks.registered::%loop-index%}%__: %{stocks.value.%{stocks.registered::%loop-index%}%}% coins%nl%  Owned: %{stocks.ownership.%{_p}%.%{stocks.registered::%loop-index%}%}% shares."
      add {stocks.value.%{stocks.registered::%loop-index%}%} * {stocks.ownership.%{_p}%.%{stocks.registered::%loop-index%}%} to {_net_worth}
    set {_m} to "%{_m}%%nl%%nl%**NET WORTH IN STOCKS**%nl%%{_net_worth}% coins"

    set {_msg} to a message builder
    append line {@linkDefaultedMsg} if {_link_defaulted} is true
    append line {_m}

    reply with {_msg}

discord command stockbalance [<text>]:
  prefixes: {@prefixes}
  aliases: stockbal
  trigger:
    if arg-1 is not set:
      reply with "s2!stockbalance <ticker>"
      stop
    set {_ticker} to arg-1 in upper case
    if {stocks.registered::*} does not contain {_ticker}:
      reply with "Stock not registered!"
    
    reply with "%{_ticker}%: %{stocks.balance.%{_ticker}%}%"

discord command lastplayed [<OfflinePlayer>]:
  prefixes: {@prefixes}
  trigger:
    set {_p} to arg-1
    set {_link_defaulted} to false

    if arg-1 is not set:
      if {discord_link.link.discord.%discord id of author of event-message%} is set:
        set {_p} to {discord_link.link.discord.%discord id of author of event-message%}
        set {_link_defaulted} to true
      else:
        reply with {@specifyPlayer}
        stop
    
    set {_msg} to a message builder
    append line "**Last Played %{_p}%**"
    append line ""
    append line "__Last Joined__"
    append line "%{last_played.%{_p}%.join}%"
    append line "%difference between now and {last_played.%{_p}%.join}% ago"
    append line ""
    append line "__Last Left__"
    append line "%{last_played.%{_p}%.leave}%"
    append line "%difference between now and {last_played.%{_p}%.leave}% ago"
    reply with {_msg}

discord command playtime:
  prefixes: {@prefixes}
  aliases: pt
  trigger:
    reply with "merged into stats command"


discord command locations [<text>]:
  prefixes: {@prefixes}
  aliases: locs
  trigger:
    set {_search_terms::*} to arg-1 otherwise "" split at "|"

    set {_msg} to a message builder
    append line "**SERVER LOCATIONS**%nl%__Search Term:__ %arg-1%%nl%"
    loop {locations::*}:
      loop {_search_terms::*}:
        if loop-index-1 contains loop-value-2:
          append line "`%loop-index-1%` â†’ %loop-value-1's x-coordinate%, %loop-value-1's y-coordinate%, %loop-value-1's z-coordinate% in %world of loop-value-1%"
          continue
    
    reply with {_msg}


discord command tps:
  prefixes: {@prefixes}
  trigger:
    set {_tps} to tps from last minute
    reply with "**Server TPS:** %{_tps}%"

discord command ping [<player>]:
  prefixes: {@prefixes}
  trigger:
    if arg-1 is not set:
      reply with "ping <player>"
      stop
    set {_ping} to arg-1's ping
    reply with "**Ping of %arg-1%:** %{_ping}%ms"


discord command time:
  prefixes: {@prefixes}
  trigger:
    reply with "It is currently `%time in world ""world""%` in the overworld."