options:
  discordChannelId: 711284320890781757 # economy logs channel

every 5 seconds:
  send {treasury.logs.pending} to channel with id "{@discordChannelId}" with "Cheezy"
  set {treasury.logs.pending} to ""

function logTreasuryAction(a: text):
  set {treasury.logs.pending} to "%{treasury.logs.pending}%%nl%[%now% UTC] %{_a}%"

function withdrawFromTreasury(p: player, a: number):
  format slot 4 of {_p} with lime stained glass pane named "§a[✓]" with lore "§rI confirm that I am not" and "§rembezzling public funds." to be unstealable
  wait 5 ticks
  remove {_a} from {treasury.balance}
  add {_a} to {bank.balance::%{_p}%}
  send "§2Moved §a%{_a}%§2 coins from the treasury to your balance." to {_p}
  logTreasuryAction("`%{_p}%` withdrew `%{_a}%` coins from the treasury")
  close {_p}'s inventory

command /treasurybalance:
  aliases: /treasurybal
  trigger:
    send "§6☭--§e§lTREASURY OF CHEEZSURV2§6--☭"
    send "§e%{treasury.balance}%§e coins"

command /treasurywithdraw <number>:
  aliases: /treasurywith
  trigger:
    if player is not op:
      send "§cInsufficient permissions!"
      stop
    
    if arg-1 <= 0:
      send "§cCannot withdraw negative or zero amount!"
      stop

    if {treasury.balance} < arg-1:
      send "§cInsufficient funds in the treasury!"
      stop
    
    open chest with 1 row named "Withdrawing %arg-1%c..." to player
    loop 9 times:
      format slot loop-number - 1 of player with light gray stained glass pane named " " to be unstealable
    format slot 4 of player with red stained glass pane named "§c[X]" with lore "§rI confirm that I am not" and "§rembezzling public funds." to run [withdrawFromTreasury(player, arg-1)]

command /treasurydeposit <number>:
  aliases: /treasurydep
  trigger:
    if player is not op:
      send "§cInsufficient permissions!"
      stop
    
    if arg-1 <= 0:
      send "§cCannot deposit negative or zero amount!"
      stop

    if {bank.balance::%player%} < arg-1:
      send "§cInsufficient funds in your balance!"
      stop
    
    remove arg-1 from {bank.balance::%player%}
    add arg-1 to {treasury.balance}
    send "§2Moved §a%arg-1%§2 coins from the your balance to the treasury."
    logTreasuryAction("`%player%` deposited `%arg-1%` coins to the treasury")