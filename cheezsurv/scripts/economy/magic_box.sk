options:
  magicBoxName: §bMagic Box
  magicBoxLore: §7Right click to redeem and use /magicbox to open.

function openMagicBox(p: player):
  remove 1 from {magicboxes.%{_p}%}

  open chest with 3 rows named "Magic Box Loot" to {_p}
  play sound "block.barrel.open" to {_p}

  loop random integer between 1 and 5 times:
    set {_amount} to random integer between 2 and 8
    format slot random integer between 0 and 26 of {_p} with giveBanknote({_amount}, 1) to close # then run [send "quack" to {_p}]

command /magicboxgive [<integer=1>] [<offlineplayer>]:
  trigger:
    if player is not op:
      send "§cInsufficient permissions!"
      stop
    
    if arg-2 is not set:
      send "§2Gave §a%arg-1%§2 magic boxes to all players."
      loop all players:
        add arg-1 to {magicboxes.%loop-player%}
        send "§2You have been given §a%arg-1%§2 magic boxes." to loop-player
    else:
      send "§2Gave §a%arg-1%§2 magic boxes to §a%arg-2%§2."
      add arg-1 to {magicboxes.%arg-2%}
      send "§2You have been given §a%arg-1%§2 magic boxes." to arg-2

command /magicboxes <offlineplayer=%player%>:
  trigger:
    set {magicboxes.%arg-1%} to 0 if {magicboxes.%arg-1%} is not set
    send "§e%arg-1% §6has §e%{magicboxes.%arg-1%}%§6 available magic boxes."

command /magicboxget <integer=1>:
  trigger:
    if player is not op:
      send "§cInsufficient permissions!"
      stop
    give player arg-1 of shiny barrel named "{@magicBoxName}" with lore "{@magicBoxLore}"

command /magicbox:
  trigger:
    if {magicboxes.%player%} <= 0:
      send "§6You have §e%{magicboxes.%player%}%§6 available magic boxes."
      stop
    open chest with 1 row named "§cM§6a§eg§2i§3c §9B§5o§9x§3e§2s" to player
    loop 9 times:
      format slot loop-number - 1 of player with light gray stained glass pane named " " to be unstealable
    format slot 4 of player with shiny barrel named "§2Open Magic Box" with lore "§a%{magicboxes.%player%}%§a available" to run [openMagicBox(player)]

on right click:
  name of player's tool is "{@magicBoxName}"
  cancel event
  remove 1 of player's tool from player
  add 1 to {magicboxes.%player%}
  send "§6Redeemed §e1§6 magic box. §e/magicbox"

on join:
  {magicboxes.%player%} > 0
  send "§6You have §e%{magicboxes.%player%}%§6 available magic boxes! §e/magicbox§6 to open!"