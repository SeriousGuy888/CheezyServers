options:
  discordChannelId: %{config.discord_chat}%

on death of villager:
  if victim has scoreboard tag "no_villager_death_message":
    stop
    
  set {_villager_name} to display name of victim otherwise "Villager"
  if attacker is set:
    set {_attacker_name} to display name of attacker ? ("%attacker%" in title case)

  if victim is leashed:
    if attacker is set:
      set {villager_death_messages::%uuid of victim%} to "%{_villager_name}% was hanged by %{_attacker_name}%"
    else:
      set {villager_death_messages::%uuid of victim%} to "%{_villager_name}% was executed by hanging"
  else:
    set {_murder_variations::*} to ("murdered", "jakobised", "assassinated", "slaughtered", "nuked", "deaced", "terminated", "deprecated", "annihilated", "killed to death", "put down", "euthanised", "butchered", "destroyed", "purged", "massacred", "executed", "deleted")
    set {_died_variations::*} to ("deaced", "passed away", "was removed from existence", "contracted death syndrome")

    if attacker is set:
      set {villager_death_messages::%uuid of victim%} to "%{_villager_name}% was %random element out of {_murder_variations::*}% by %{_attacker_name}%"
    else:
      set {villager_death_messages::%uuid of victim%} to "%{_villager_name}% %random element out of {_died_variations::*}% from %damage cause%"
  
  set {_nloc::*} to nearestLocation(location of victim) split at "|"
  set {villager_death_messages::%uuid of victim%} to "%{villager_death_messages::%uuid of victim%}% §7§o[%round({_nloc::2} parsed as number)%§7§om from %{_nloc::1} in uppercase%]"


  send ":skull: **%uncoloured {villager_death_messages::%uuid of victim%}% at %floor(x-coordinate of victim)% %floor(y-coordinate of victim)% %floor(z-coordinate of victim)% in %world of victim%**" to channel with id "{@discordChannelId}" with "Cheezy"
  loop all players:
    set {villager_death_messages.subscribed.%loop-player%} to true if {villager_death_messages.subscribed.%loop-player%} is not set
    {villager_death_messages.subscribed.%loop-player%} is true
    send "%{villager_death_messages::%uuid of victim%}%" to loop-player
  
  delete {villager_death_messages::%uuid of victim%}

on spawn of villager:
  world of event-entity is "creative"
  add "no_villager_death_message" to scoreboard tags of event-entity

command /villagerdeathmessages:
  aliases: /villdeathmsgs, /vdm
  trigger:
    set {villager_death_messages.subscribed.%player%} to true if {villager_death_messages.subscribed.%player%} is not set
    if {villager_death_messages.subscribed.%player%} is true:
      set {villager_death_messages.subscribed.%player%} to false
      send "§aUnsubscribed from villager death messages."
    else if {villager_death_messages.subscribed.%player%} is false:
      set {villager_death_messages.subscribed.%player%} to true
      send "§aSubscribed to villager death messages."