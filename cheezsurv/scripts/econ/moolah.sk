options:
  moolahName: §6Fool's Gold Coin
  moolahLore: §eDO NOT RENAME

  discordChannelId: 711284320890781757 # economy logs channel



every 3 seconds:
    send {economy.logs.pending} to channel with id "{@discordChannelId}" with "Cheezy"
    set {economy.logs.pending} to ""

every 10 seconds:
  loop all players:
    set {bank.balance.%loop-player%} to 0 if {bank.balance.%loop-player%} is not set

function logAction(a: text):
  set {economy.logs.pending} to "%{economy.logs.pending}%%nl%[%now% UTC] %{_a}%"


command /getmoolah <number>:
  trigger:
    player has permission "*"
    give arg-1 of sunflower named "{@moolahName}" with lore "{@moolahLore}" to player

on right click:
  name of player's tool is "{@moolahName}"
  cancel event

  set {_deposited} to 0

  # if player is not sneaking:
  remove 1 from item amount of player's tool
  add 1 to {_deposited}
  # else if player is sneaking:
  #   set {_coins} to amount of player's held item in player's inventory

  #   loop all items in player's inventory:
  #     loop-item is sunflower
  #     if name of loop-item is "{@moolahName}":
  #       add item amount of loop-item to {_deposited}
  #       remove loop-item from player
  #     else:
  #       send "b"


  
  add {_deposited} to {bank.balance.%player%}
  send action bar "§3Deposited §b%{_deposited}%§3 coins. §b/bal" to player
  play sound "block.note_block.pling" at volume 0.25 to player

  logAction("`%player%` deposited `%{_deposited}%` coins")

command /balance <OfflinePlayer=%player%>:
  aliases: /bal
  trigger:
    send "§3Bank balance of §b%arg-1%§3 is §b%{bank.balance.%arg-1%}% §3coins."

command /withdraw [<integer>]:
  aliases: /with
  trigger:
    if arg-1 is not set:
      send "§c/with <amount>"
      stop
    if {bank.balance.%player%} is greater than or equal to arg-1:
      if player's inventory cannot hold arg-1 of sunflower:
        send "§cInsufficient inventory space!"
        logAction("`%player%` attempted and failed to withdraw `%arg-1%` coins - Insufficient inventory space")
        stop
      give arg-1 of sunflower named "{@moolahName}" with lore "{@moolahLore}" to player
      remove arg-1 from {bank.balance.%player%}
      logAction("`%player%` withdrew `%arg-1%` coins")
    else:
      send "§cInsufficient funds! /bal"
      logAction("`%player%` attempted and failed to withdraw `%arg-1%` coins - Insufficient funds")


command /circulation:
  trigger:
    set {_amount} to 0
    loop all offline players:
      add {bank.balance.%loop-offlineplayer%} to {_amount}
    send "§3There are §b%{_amount}%§3 coins in circulation in people's bank balances."